@name WestAusMan98's Ditch Light Relay E2
@inputs OnFront OnRear Horn
@outputs PortFront StarboardFront PortRear StarboardRear RGB:vector
@persist OnPeriod FlashDuration OscillateFront OscillateRear CanStartFront CanStartRear

if(first()|duped()){
    #V2
    
    #Setup Instructions
    #1: Configure the E2 to your preference.
    #2: Spawn this E2 around where all your other locomotive E2s are placed (electrical cabinet.etc).
    #3: Spawn two HeadSplight E2s and put them on either the front or rear of the loco (rear ditch lights are optional, front ones are mandatory).
    #4: Wire the "On" input to a light switch of some sort.
    #5: Wire "Horn" to whatever your horn key is (this would most likely be on your Pod Controller).
    #6: Wire the left side HeadSplight E2 to the "Port" output and the right side one to the "Starboard" output.
    #7: Wire "RGB" on the HeadSplight E2s to the "RGB" output on this E2 (OPTIONAL).
    #8: Repeat steps 2 through 7 for the rear if your loco has rear ditch lights.
    #9: Done!
        
    #Config
    OnPeriod = 0.5 #How long one light will remain on before switching to the other. Around half a second (0.5) is the norm, Wisconin Central and KiwiRail would have 0.3 and MetroLink use 1.
    FlashDuration = 30 #How long until the ditch lights stop oscillating. 30 seconds is the golden standard.
    RGB = vec(255,255,192) #Color of the lights.
}

#Front
if(!OscillateFront){
    stoptimer("flash_init_front")
    stoptimer("flash_left_front")
    stoptimer("flash_right_front")
}

if(OnFront && !OscillateFront){
    PortFront = 1
    StarboardFront = 1
    CanStartFront = 1
}

if(Horn & ~Horn && CanStartFront == 1){
    OscillateFront = 1
}

if(OscillateFront && CanStartFront == 1){
    timer("flash_init_front",OnPeriod*1000)
    timer("on_front",FlashDuration*1000)
    CanStartFront = 0
    if(OnFront){
        PortFront = 1
        StarboardFront = 0
    }
}

if(Horn & ~Horn && CanStartFront == 0){
    stoptimer("on_front")
    timer("on_front",FlashDuration*1000)
}

if(clk("flash_init_front")){
    PortFront = 0
    StarboardFront = 1
    timer("flash_right_front",OnPeriod*1000)
}

if(clk("flash_right_front")){
    PortFront = 1
    StarboardFront = 0
    timer("flash_left_front",OnPeriod*1000)
}

if(clk("flash_left_front")){
    PortFront = 0
    StarboardFront = 1
    timer("flash_right_front",OnPeriod*1000)
}

if(clk("on_front")){
    if(OnFront){
        PortFront = 1
        StarboardFront = 1
    }
    stoptimer("flash_init_front")
    stoptimer("flash_left_front")
    stoptimer("flash_right_front")
    OscillateFront = 0
    CanStartFront = 1
}

if(!OnFront){
    stoptimer("flash_init_front")
    stoptimer("flash_left_front")
    stoptimer("flash_right_front")
    OscillateFront = 0
    PortFront = 0
    StarboardFront = 0
    CanStartFront = 0
}

if(!OscillateRear){
    stoptimer("flash_init_rear")
    stoptimer("flash_left_rear")
    stoptimer("flash_right_rear")
}

#Rear
if(OnRear && !OscillateRear){
    PortRear = 1
    StarboardRear = 1
    CanStartRear = 1
}

if(Horn & ~Horn && CanStartRear == 1){
    OscillateRear = 1
}

if(OscillateRear && CanStartRear == 1){
    timer("flash_init_rear",OnPeriod*1000)
    timer("on_rear",FlashDuration*1000)
    CanStartRear = 0
    if(OnRear){
        PortRear = 1
        StarboardRear = 0
    }
}

if(Horn & ~Horn && CanStartRear == 0){
    stoptimer("on_rear")
    timer("on_rear",FlashDuration*1000)
}

if(clk("flash_init_rear")){
    PortRear = 0
    StarboardRear = 1
    timer("flash_right_rear",OnPeriod*1000)
}

if(clk("flash_right_rear")){
    PortRear = 1
    StarboardRear = 0
    timer("flash_left_rear",OnPeriod*1000)
}

if(clk("flash_left_rear")){
    PortRear = 0
    StarboardRear = 1
    timer("flash_right_rear",OnPeriod*1000)
}

if(clk("on_rear")){
    if(OnRear){
        PortRear = 1
        StarboardRear = 1
    }
    stoptimer("flash_init_rear")
    stoptimer("flash_left_rear")
    stoptimer("flash_right_rear")
    OscillateRear = 0
    CanStartRear = 1
}

if(!OnRear){
    stoptimer("flash_init_rear")
    stoptimer("flash_left_rear")
    stoptimer("flash_right_rear")
    OscillateRear = 0
    PortRear = 0
    StarboardRear = 0
    CanStartRear = 0
}
